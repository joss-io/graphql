
buildscript {

  repositories {
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
    jcenter()
  }

  dependencies {
    classpath "net.ltgt.gradle:gradle-apt-plugin:0.21"
  }

}

allprojects {
  group = 'com.github.zourzouvillys.graphql'
  version = 'master-SNAPSHOT'
}

subprojects {

  apply plugin: 'java-library'
  apply plugin: 'eclipse'
  apply plugin: 'net.ltgt.apt'
  apply plugin: 'net.ltgt.apt-eclipse'
  apply plugin: 'maven'

  apply from: "${rootDir}/gradle/deps.gradle"

  repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://jitpack.io' }
  }

  sourceCompatibility = 11
  targetCompatibility = 11

// --------------------------------------------------
// compile
// --------------------------------------------------


tasks.withType(JavaCompile) {
    options.compilerArgs << '-parameters'
}


eclipse {
  jdt {
    file {
      withProperties { properties ->
        properties.setProperty("org.eclipse.jdt.core.compiler.codegen.methodParameters", "generate")
      }
    }
  }
}

  // --------------------------------------------------
  // install
  // --------------------------------------------------


  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  tasks.withType(Javadoc) {
      options.addStringOption('Xdoclint:none', '-quiet')
  }
    
  task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  javadoc {
    options.addBooleanOption('html5', true)
  }  

  artifacts {
    archives jar
    archives sourcesJar
    //archives javadocJar
  }

  javadoc {
    title = "ZRZ GraphQL"
    failOnError = false
    exclude "**/impl/**";
  }

  // To specify a license in the pom:
  install {
    repositories.mavenInstaller {
      pom.project {
        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
          }
        }
      }
    }
  }
  
}

wrapper {
    gradleVersion = '6.8.2'
}
